1 FLASH 0:OVER 0:WINDOW#0;420,20,45,225:FOR n=1TO 2:WINDOW #n;435,210,45,15:END FOR n:MODE 8
10 REMark LOS LOBOS FANTASMAS                 BIT SOFT 1988,1992
20 RESTORE :CLEAR
30 empezar
40 REPeat bucle
50 rutina_principal
60 IF ro=11 THEN EXIT bucle
70 END REPeat bucle
80 REMark --- Fin del juego ---
90 PRINT \"≥≥Lo has conseguido!!"
100 espera
110 PRINT \" Esa era la salida del castillo"
120 espera
130 PRINT \\"≥Has tenido Éxito, ";nombre$;"!"
140 PRINT \" saliendo del castillo"
150 espera
160 PRINT \"      ≥Bien hecho!"
170 espera
180 PRINT \"  Tu puntuaciñn es ";
190 PRINT 39*cuenta+51*fuerza+21*riqueza+comida+309*mk+45*traje+100*amuleto+17*espada+7*hacha
200 STOP
210 REMark ------------------
220 DEFine PROCedure rutina_principal
230 fuerza=fuerza-5
240 IF fuerza<25 AND fuerza>=1 THEN 
250 PRINT "   Peligro, ";nombre$;", tu fuerza"
260 PRINT "   ≥te estå abandonando!"
270 END IF 
280 IF fuerza<1 THEN 
290 PRINT " Tu fuerza te ha abandonado..."\"    y ahora estås muerto..."
300 GO TO 170
310 END IF 
320 cuenta=cuenta+1
330 PRINT \\" ";nombre$;", tu fuerza es ";fuerza
340 IF riqueza>0 THEN PRINT "  Tienes ";riqueza;"$"
350 IF comida>0 THEN PRINT " Tu saco de provisiones contiene"\"         ";comida;" unidades de comida"
360 IF traje=1 THEN PRINT " Llevas puesta la armadura"
370 IF hacha<>0 OR espada<>0 OR amuleto<>0 THEN 
380 PRINT " Llevas ";
390 IF hacha=1 THEN PRINT "un hacha ";
400 IF espada=1 THEN PRINT "una espada ";
410 IF espada+hacha>0 AND amuleto=1 THEN PRINT " y ";
420 IF amuleto=1 THEN PRINT !"el"!"amuleto"!"mågico"
430 END IF 
440 PRINT
450 IF luz=0 THEN 
460 PRINT "Estå demasiado oscuro para poder ver"
470 ELSE 
480 descripcion_sala
490 END IF 
500 k=a(ro,7)
510 IF k>9 THEN INK 2:PAPER 7:PRINT \" Hay un tesoro que vale ";k;"$":INK 6:PAPER 0:END IF 
520 IF k<0 THEN 
530 BEEP 1000,1:PAUSE 10:BEEP 1000,2:PAUSE 10:BEEP 1000,3
540 INK 0:PAPER 5
550 PRINT \\\"≥≥≥Peligro!!! Hay un monstruo aquì..":espera
560 IF k=-1 THEN m$="Feroz Lobo Fantasma":ff=5
570 IF k=-2 THEN m$="Fanåtico estrangulador":ff=10
580 IF k=-3 THEN m$="MalÉvolo Zombie":ff=15
590 IF k=-4 THEN m$="Devastador Dragon Helado":ff=20
600 PRINT \" Este es un ";m$
610 PRINT \"≥≥El nivel de peligro es ";ff;"!!"
620 PRINT "                                   "
630 INK 6:PAPER 0
640 END IF 
650 espera
660 FLASH 1
670 PRINT \\"¥QuÉ vas a hacer?(? AYUDA) "
680 FLASH 0
690 REPeat mover
700 mo$=INKEY$
705 IF mo$="?" THEN AYUDA:GO TO 520
710 IF k<0 AND (mo$<>"l" AND mo$<>"L" AND mo$<>"h" AND mo$<>"H") THEN NEXT mover
720 IF mo$<>"" THEN EXIT mover
730 END REPeat mover
740 INK 1
750 PRINT \"------------------------------------"
760 INK 6
770 PRINT
780 IF (mo$=="Q") THEN GO TO 180
785 IF mo$=="?" THEN AYUDA
790 IF (mo$=="N") AND a(ro,1)=0 THEN PRINT " No hay salida por ese camino":GO TO 650
800 IF (mo$=="S") AND a(ro,2)=0 THEN PRINT " No hay salida por el sur":GO TO 650
810 IF (mo$=="E") AND a(ro,3)=0 THEN PRINT " No se puede ir en esa direcciñn":GO TO 650
820 IF (mo$=="O") AND a(ro,4)=0 THEN PRINT " No puedes atravesar las piedras":GO TO 650
830 IF (mo$=="A") AND a(ro,5)=0 THEN PRINT " No puedes subir desde aquì":GO TO 650
840 IF (mo$=="B") AND a(ro,6)=0 THEN PRINT " No puedes descender desde aquì":GO TO 650
850 IF (mo$=="H") AND RND>.7 THEN 
860 PRINT " No,tienes que quedarte y luchar"
870 mo$="l"
880 espera
890 END IF 
900 IF mo$=="H" THEN 
910 PRINT "¥Hacia donde quieres correr?"
920 espera
930 k=0
940 mo$=""
950 GO TO 680
960 END IF 
970 IF (mo$=="L") AND a(ro,7)>-1 THEN 
980 PRINT "≥Aquì no hay nadie con quien luchar!"
990 GO TO 650
1000 END IF 
1010 IF mo$=="I" THEN inventario
1020 IF (mo$=="C") AND comida=0 THEN 
1030 PRINT "≥No tienes comida!"
1040 GO TO 650
1050 END IF 
1060 IF mo$=="T" THEN tesoro
1070 IF mo$=="L" THEN luchar
1080 IF mo$=="M" THEN 
1090 REPeat elegir
1100 ro=RND(1 TO 19)
1110 IF ro<>6 AND ro<>11 THEN EXIT elegir
1120 END REPeat elegir
1130 amuleto=0:REMark El amuleto                          solo se usa                         una vez
1140 END IF 
1150 IF mo$=="C" THEN comer
1160 IF mo$=="N" THEN ro=a(ro,1)
1170 IF mo$=="S" THEN ro=a(ro,2)
1180 IF mo$=="E" THEN ro=a(ro,3)
1190 IF mo$=="O" THEN ro=a(ro,4)
1200 IF mo$=="A" THEN ro=a(ro,5)
1210 IF mo$=="B" THEN ro=a(ro,6)
1220 END DEFine rutina_principal
1230 REMark --------------
1240 DEFine PROCedure luchar
1250 FOR j=1 TO 30
1260 BEEP 100,j:BEEP 100,200-j
1270 END FOR j
1280 IF INKEY$<>"" THEN GO TO 1280
1290 FLASH 1:PRINT \"  Pulse una tecla para luchar...":FLASH 0
1300 REPeat teclea
1310 IF INKEY$<>"" THEN EXIT teclea
1320 BEEP 100,5:END REPeat teclea
1330 FOR j=1 TO 6
1340 INK RND(1 TO 7)
1350 PRINT \" * - * - * - * - * - * - * - *"
1360 END FOR j
1370 INK 6
1380 IF traje=1 THEN 
1390 PRINT " Tu armadura aumenta tus            probabilidades de Éxito"
1400 ff=3*(INT(ff/4))
1410 espera
1420 END IF 
1430 IF hacha=0 AND espada=0 THEN 
1440 PRINT " No tienes armas y debes luchar con  las manos desnudas"
1450 ff=INT(ff+ff/5)
1460 espera
1470 END IF 
1480 IF hacha<>0 OR espada<>0 THEN 
1490 IF hacha=1 AND espada=0 THEN 
1500 PRINT " Solo tienes un hacha para luchar"
1510 ff=4*INT(ff/5)
1520 END IF 
1530 IF hacha=0 AND espada=1 THEN 
1540 PRINT " debes luchar con                       tu espada"
1550 ff=3*INT(ff/4)
1560 END IF 
1570 IF hacha=1 AND espada=1 THEN 
1580 PRINT "¥QuÉ arma? 1 - Hacha  2 - Espada"
1590 REPeat elegir
1600 arma$=INKEY$
1610 IF arma$="1" OR arma$="2" THEN EXIT elegir
1620 END REPeat elegir
1630 z=arma$
1640 IF z=1 THEN ff=4*INT(ff/5)
1650 IF z=2 THEN ff=3*INT(ff/4)
1660 END IF 
1670 END IF 
1680 REMark -- La Batalla --
1690 PRINT \
1700 REPeat follon
1710 PRINT:FLASH RND (0 TO 1)
1720 INK RND(3 TO 7)
1730 BEEP 1000,RND(10 TO 212)
1740 BEEP 1000,RND(10 TO 212)
1750 IF RND>.5 THEN 
1760 PRINT " El ";m$;" ataca"
1770 ELSE 
1780 PRINT "Tu atacas al ";m$
1790 END IF 
1800 espera
1810 IF RND>.5 THEN 
1820 PRINT "≥Estås ganåndole!"
1830 ff=INT(5*ff/6)
1840 END IF 
1850 espera
1860 IF RND>.5 THEN 
1870 PRINT "≥El monstruo te ha ganado!"
1880 fuerza=fuerza-5
1890 END IF 
1900 IF RND<.35 THEN EXIT follon
1910 END REPeat follon
1920 INK 6
1930 IF RND>ff/10 THEN 
1940 PRINT \"≥≥Estås matando al                   ";m$;"!!"
1950 mk=mk+1
1960 fuerza=fuerza+INT(fuerza/2)
1970 ELSE 
1980 PRINT \" El ";m$!" te ha "!"derrotado..."
1990 fuerza=INT(fuerza/2)
2000 END IF 
2010 a(ro,7)=0:espera:espera
2020 FLASH 0
2030 END DEFine lucha
2040 REMark -----------------
2050 DEFine PROCedure descripcion_sala
2060 INK 2
2070 PRINT \" ^V^V^V^V^V^V^V^V^V^V^V^V^V^V^V^V                                      "
2080 INK 6
2090 SELect ON ro
2100 =1
2110 PRINT " Estås en el vestìbulo"
2120 PRINT " Hay una puerta al sur, y"
2130 PRINT " a travÉs de las ventanas del norte"
2140 PRINT " puedes ver un jardìn secreto."
2150 =2
2160 PRINT " Esta es la Sala de Audiencias."
2170 PRINT " Hay una ventana al oeste."
2180 PRINT " Hacia la derecha puedes ver"
2190 PRINT " la entrada del castillo."
2200 PRINT " Hay puertas hacia el norte este    y sur"
2210 =3
2220 PRINT " Estås en el Gran Salñn,una sala"
2230 PRINT " en forma de L. Hay puertas al este"
2240 PRINT " y al norte. Hay"
2250 PRINT " una puerta al oeste en                la alcoba"
2260 =4
2270 PRINT " Esta es la sala privada de "
2280 PRINT " Audiencias del Monarca.Hay una sola salida al sur"
2290 =5
2300 PRINT " Este pequeâo recibidor tiene una"
2310 PRINT " puerta al norte, y otra al oeste,"
2320 PRINT " y una escalera de caracol que"
2330 PRINT " pasa a travÉs de la sala. Puedes"
2340 PRINT " ver un lago ornamental a travÉs de"
2350 PRINT " las ventanas del sur"
2360 =6
2370 PRINT " Estås en la entrada del"
2380 PRINT " castillo de piedra prohibido."
2390 =7
2400 PRINT " Esta es la cocina del castillo."
2410 PRINT " Por las ventanas del muro norte"
2420 PRINT " puedes ver un jardìn secreto."
2430 PRINT " Una puerta sale de la cocina hacia el sur"
2440 =8
2450 PRINT " Estås en el almacÉn, rodeado de"
2460 PRINT "especias, verduras, grandes sacos"
2470 PRINT "de harina y otras provisiones."
2480 PRINT " Hay una puerta en el norte y       otra en el sur"
2490 =9
2500 PRINT " Has entrado en un ascensor..."
2510 espera
2520 PRINT "que desciende lentamente..."
2530 espera:ro=10
2540 GO TO 3080
2550 =10
2560 PRINT " Estås en el vestìbulo de atrås."
2570 PRINT " Hay unas ventanas que dan al sur"
2580 PRINT "desde la que puedes ver el"
2590 PRINT "lago ornamental. Hay una salida"
2600 PRINT "al este y otra al norte."
2610 =12
2620 PRINT "Estås en un hômedo y oscuro calabozo"
2630 PRINT " Solo hay una salida, un pequeâo"
2640 PRINT " agujero en el muro hacia el oeste."
2650 =13
2660 PRINT " Te encuentras en la sala de la"
2670 PRINT "guarida de la prisiñn, en el sñtano"
2680 PRINT "del castillo. La escalera de caracol"
2690 PRINT " termina en esta sala. Hay otra"
2700 PRINT " salida, un pequeâo agujero en el    muro este."
2710 =14
2720 PRINT " Estås en el dormitorio principal en"
2730 PRINT " el piso superior del castillo..."
2740 PRINT " Mirando desde la ventana hacia el"
2750 PRINT " oeste, puedes ver la entrada del"
2760 PRINT " castillo, mientras que el jardìn"
2770 PRINT " secreto es visible desde la ventana"
2780 PRINT " norte. Hay puertas al "
2790 PRINT " este y al sur."
2800 =15
2810 PRINT " Este es el vestìbulo superior."
2820 PRINT " Hay una puerta al norte, y"
2830 PRINT " puedes ver tambiÉn una escalera."
2840 PRINT " A travÉs de la ventana sur"
2850 PRINT " le llega el reflejo del lago."
2860 =16
2870 PRINT " Esta sala fuÉ usada como sala del"
2880 PRINT " tesoro del castillo hace muchos aâos"
2890 PRINT " No tiene ventanas, solo salidas"
2900 PRINT " al este y al norte"
2910 =17
2920 PRINT "≥≥Ooh!!Estås en el dormitorio de las"
2930 PRINT " doncellas. Hay una salida al"
2940 PRINT " oeste y una puerta al sur"
2950 =18
2960 PRINT " Esta pequeâa habitaciñn del piso"
2970 PRINT " superior es el vestidor. Hay una"
2980 PRINT " ventana al norte, con una vista del"
2990 PRINT " jardìn bajo ella."
3000 PRINT " Una puerta te lleva al sur."
3010 =19
3020 PRINT " Esta es una pequeâa sala fuera del"
3030 PRINT " ascensor del castillo, a la que se"
3040 PRINT " accede por una puerta al norte.Otra"
3050 PRINT " puerta queda al oeste. Puedes"
3060 PRINT " ver el lago a travÉs de"
3070 PRINT " las ventanas del sur."
3080 END SELect 
3090 END DEFine descripcion_sala
3100 REMark ------------------
3110 DEFine PROCedure tesoro
3120 FOR j=1 TO 60
3130 BEEP 101,j
3140 END FOR j
3150 IF a(ro,7)<10 THEN 
3160 PRINT "No hay tesoro que coger"
3170 espera
3180 END IF 
3190 IF luz=0 THEN 
3200 PRINT " No puedes ver donde estå"
3210 espera
3220 END IF 
3230 IF luz=1 AND a(ro,7)>9 THEN 
3240 riqueza=riqueza+a(ro,7)
3250 a(ro,7)=0
3260 PRINT \"≥Has cogido el tesoro!"
3270 espera
3280 END IF 
3290 END DEFine tesoro
3300 REMark ---------------
3310 DEFine PROCedure comer
3320 FOR j=1 TO 20
3330 BEEP 500,200+j:BEEP 500,200-j
3340 END FOR j
3350 CLS
3360 IF comida>=1 THEN 
3370 PRINT " Tienes ";comida;" unidades de comida"
3380 PRINT "¥Cuanto quieres comer?"
3390 REPeat como_comida
3400 PRINT "         ";
3410 INPUT z
3420 IF z<=comida THEN EXIT como_comida
3430 END REPeat como_comida
3440 comida=INT(comida-z)
3450 fuerza=INT(fuerza+5*z)
3460 espera
3470 END IF 
3480 END DEFine comer
3490 REMark ------------
3500 DEFine PROCedure empezar
3510 BORDER 0,0:PAPER 0:INK 6
3511 CLS:CLS#0
3512 STRIP 3,4,3:CSIZE 3,1:AT 1,12:PRINT "LOS":AT 3,11:PRINT "LOBOS":AT 5,10:PRINT "FANTASMAS":CSIZE 0,0:STRIP 0:AT 20,0:PRINT "PULSE TECLA":PRINT #0;" BIT SOFT 1988,1992":A$=INKEY$(-1)
3520 CLS:CLS#0
3530 fuerza=100
3540 riqueza=75
3550 comida=0
3560 cuenta=0
3570 mk=0:REMark Nômero de monstruos                     muertos
3580 REMark ------------
3590 REMark Preparar castillo
3600 DIM a(19,7):RESTORE 4710
3610 FOR b=1 TO 19
3620 FOR c=1 TO 7
3630 READ a(b,c)
3640 END FOR c
3650 BEEP 1000,5*b
3660 END FOR b
3670 INPUT \\"¥Cual es tu nombre, explorador?"\nombre$
3680 CLS
3690 ro=6:REMark Posiciñn de partida                (ro=nômero de sala)
3700 espada=0
3710 amuleto=0
3720 traje=0
3730 luz=0
3740 hacha=0
3750 REMark ----------------
3760 REMark Repartir el tesoro
3770 FOR j=1 TO 4
3780 REPeat repartir
3790 m=RND(1 TO 19)
3800 IF m<>6 AND m<>11 AND a(m,7)=0 THEN EXIT repartir
3810 END REPeat repartir
3820 a(m,7)=RND (10 TO 110)
3830 END FOR j
3840 REMark ----------------
3850 REMark Repartir monstruos
3860 FOR j=1 TO 4
3870 REPeat repartir
3880 m=RND(1 TO 18)
3890 IF m<>6 AND m<>11 AND a(m,7)=0 THEN EXIT repartir
3900 END REPeat repartir
3910 a(m,7)=-j
3920 END FOR j
3930 a(4,7)=RND (100 TO 199)
3940 a(16,7)=RND(100 TO 199)
3950 END DEFine empezar
3960 REMark -------------
3970 DEFine PROCedure inventario
3980 trampa=0
3990 PRINT " Provisiones e Inventario"
4000 REPeat provisiones
4010 espera
4020 IF riqueza=0 THEN EXIT provisiones
4030 IF riqueza>=.1 THEN 
4040 PRINT \\"     Tienes ";riqueza;"$"
4050 PRINT " Puedes comprar 1 - Antorcha (15$)"
4060 PRINT "                2 - Hacha (10$)"
4070 PRINT "                3 - Espada (20$)"
4080 PRINT "                4 - Comida (2$ unid)"
4090 PRINT "                5 - Amuleto (30$)"
4100 PRINT "                6 - Armadura (50$)"
4110 PRINT "                0 - Continuar juego"
4120 FLASH 1
4130 PRINT \"   Elija nômero"
4140 FLASH 0
4150 IF INKEY$<>"" THEN GO TO 4150
4160 REPeat num
4170 z$=INKEY$
4180 IF z$>="0" AND z$<="6" THEN EXIT num
4190 END REPeat num
4200 z=z$
4210 IF z=0 THEN EXIT provisiones
4220 IF riqueza<0 AND z<>0 THEN trampa=1:EXIT provisiones
4230 SELect ON z
4240 =1
4250 luz=1
4260 riqueza=riqueza-15
4270 =2
4280 hacha=1
4290 riqueza=riqueza-10
4300 =3
4310 espada=1
4320 riqueza=riqueza-20
4330 =5
4340 amuleto=1
4350 riqueza=riqueza-30
4360 =6
4370 traje=1
4380 riqueza=riqueza-50
4390 =4
4400 PRINT "¥Cuantas unidades de comida?"
4410 REPeat cuantas
4420 PRINT "    ";
4430 INPUT muchas
4440 IF 2*muchas<=riqueza THEN EXIT cuantas
4450 END REPeat cuantas
4460 muchas=INT(muchas)
4470 comida=comida+muchas
4480 riqueza=riqueza-2*muchas
4490 END SELect 
4495 IF riqueza<0 AND z<>0 THEN trampa=1:EXIT provisiones
4496 ELSE 
4498 EXIT provisiones
4500 END IF 
4510 END REPeat provisiones
4520 IF trampa=1 THEN 
4530 PRINT "≥≥Has tratado de engaâarme!!"
4540 riqueza=0:traje=0:hacha=0:espada=0
4550 amuleto=0:comida=INT(comida/4)
4560 espera
4570 END IF 
4580 END DEFine inventario
4590 REMark ------------
4600 DEFine PROCedure espera
4610 IF INKEY$<>"" THEN GO TO 4610
4620 BEEP 14000,RND(1 TO 50)
4630 PAUSE 23
4640 BEEP 32000,RND(60 TO 112),255,4,5,7,0,RND(0 TO 15)
4650 PAUSE 23
4660 BEEP 32000,RND(220 TO 250),255,4,5,7,0,RND(0 TO 15)
4670 FOR paciencia=1 TO 500
4680 END FOR paciencia
4690 END DEFine espera
4700 REMark ---------------
4710 DATA 0,2,0,0,0,0,0:REMark sala 1
4720 DATA 1,3,3,0,0,0,0:REMark 2
4730 DATA 2,0,5,2,0,0,0:REMark 3
4740 DATA 0,5,0,0,0,0,0:REMark 4
4750 DATA 4,0,0,3,15,13,0:REMark 5
4760 DATA 0,0,1,0,0,0,0:REMark 6
4770 DATA 0,8,0,0,0,0,0:REMark 7
4780 DATA 7,10,0,0,0,0,0:REMark 8
4790 DATA 0,19,0,8,0,8,0:REMark 9
4800 DATA 8,0,11,0,0,0,0:REMark 10
4810 DATA 0,0,10,0,0,0,0:REMark 11
4820 DATA 0,0,0,13,0,0,0:REMark 12
4830 DATA 0,0,12,0,5,0,0:REMark 13
4840 DATA 0,15,17,0,0,0,0:REMark 14
4850 DATA 14,0,0,0,0,5,0:REMark 15
4860 DATA 17,0,19,0,0,0,0:REMark 16
4870 DATA 18,16,0,14,0,0,0:REMark 17
4880 DATA 0,17,0,0,0,0,0:REMark 18
4890 DATA 9,0,16,0,0,0,0:REMark 19
4900 DEFine PROCedure SALVAR:DELETE MDV1_LOBOS:SAVE MDV1_LOBOS:END DEFine SALVAR
32000 DEFine PROCedure AYUDA
32010 espera:FLASH 1:PRINT \TO 10;"AYUDA":FLASH 0:PRINT \"L - Luchar"\"H - Huir de un monstruo(con excep.)"\"Q - Abandonar la aventura"\"N - Ir al norte"\"E - Ir al este"\"S - Ir al sur"\"O - Ir al oeste"\"A - Ir arriba"\"B - Ir abajo"\"I - Inventario"\"C - Comer"\"T - Coger un tesoro"\"M - Invocar el amuleto mågico para  irse a otro lugar,pero solo se puede  hacer una vez"
32020 PRINT \"Pulse tecla":tecla$=INKEY$(-1):descripcion_sala:END DEFine AYUDA
